#!/usr/bin/perl

use IO::File;
use Getopt::Long;

my (%opts);
GetOptions(\%opts, 'compiler|g=s', 'help|h|?', 'output|o=s', 'preprocessor|p=s', 'verbose|v', 'testengine|r=s', 'warning|w=i@');

if ($opts{help})
{
    die <<'EOT';
    regtest [-g grcompiler] [-o logfile] [-p gdlpp] [-r regtest] [-v] 
            [-w number] [dir]
Searches for fonts to run tests on in [dir] (defaults to .) and runs them
storing log info in logfile and exiting with the number of errors

  -g grcompiler     command to use to run grcompiler [grcompiler]
  -h                print this help and exit
  -o logfile        file to log to [regtest.log]
  -p gdlpp          command to run the gdlpp []
  -r regtest        command to test result font [GrcRegressionTest]
  -v                Run compiler -q by default, this stops it
  -w number         Run compiler with -w value (may be repeated)
EOT
}

$opts{compiler} ||= 'grcompiler';
$opts{output} ||= 'regtest.log';
$ENV{'GDLPP'} = $opts{preprocessor} if (defined $opts{preprocessor});
$opt{testengine} ||= 'GrcRegressionTest';

my (@fonts) = sort glob(($ARGV[0] ? $ARGV[0] : '.') . "/*Input.ttf");
my ($outf) = IO::File->new("> $opts{output}") || die "Can't create $opts{output}";
my ($f, $errors);

foreach $f (@fonts)
{
    my ($t) = $f;
    my ($r) = $f;
    my ($n) = $f;
    $n =~ s/Input.ttf$//o;

    print "Testing Font: $n\n";
    next unless (-f "${n}Benchmark.ttf");
    next unless (-f "${n}Main.gdl");
    $r = "${n}Test.ttf";
    $t = "${n}Main.gdl";
#    $r =~ s{^.*[/\\](.*?)$}{$1}o;

    my (@gopts);
    push (@gopts, '-q') unless ($opts{verbose});
    push (@gopts, map {"-w" => $_}, @{$opts{warning}}) if (defined $opts{warning});
    system($opts{compiler}, @gopts, $t, $f, $r);
    if (-f $r)      # assume it passes if it gives a result
    {
        $t =~ s/Main.gdl$/Benchmark.ttf/o;

        unlink 'grcregtest.log';
        my ($res) = system($opts{testengine}, $r, $t);
        if ($res == -1)
        {
            print "Failed to run $opts{testengine} because $!\n";
            $errors++;
        }
        my ($logf) = IO::File->new("grcregtest.log");
        if ($logf)
        {
        
            while (<$logf>)
            { $outf->print($_); }
            $logf->close;
        }
        if ($res > 0)
        {
            $res >>= 8;
            print "job error $res\n";
            $errors += $res;
        }
        unlink $r;
    }
    else
    {
        my ($logf) = IO::File->new("gdlerr.log");
        if ($logf)
        {
            while (<$logf>)
            { $outf->print($_); }
            $logf->close;
        }
        $errors++;
    }
}

$outf->close;

print "$errors errors encountered\n" if ($errors);
exit($errors);

